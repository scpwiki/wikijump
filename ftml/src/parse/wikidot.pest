//
// parse/wikidot.pest
//
// ftml - Convert Wikidot code to HTML
// Copyright (C) 2019 Ammon Smith
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program. If not, see <http://www.gnu.org/licenses/>.
//

// Pages {{{
page = { SOI ~ paragraph* ~ EOI }
// }}}

// Paragraphs {{{
paragraph = { paragraph_inner ~ ("\n" | EOI) | just_newlines }
paragraphs_internal = { space? ~ paragraph* ~ paragraph_inner? ~ ("\n" | EOI)? }

paragraph_inner = {
    align |
    code |
    html |
    iframe |
    javascript |
    clear_float |
    collapsible |
    div |
    bullet_list |
    numbered_list |
    horizontal_line |
    quote_block |
    words
}

just_newlines = { "\n"+ }
// }}}

// Block paragraphs {{{
align = {
    ("[[>]]\n" ~ paragraph* ~ "[[/>]]") |
    ("[[<]]\n" ~ paragraph* ~ "[[/<]]") |
    ("[[=]]\n" ~ paragraph* ~ "[[/=]]") |
    ("[[==]]\n" ~ paragraph* ~ "[[/==]]")
}

code = {
    "[[" ~ space? ~ ^"code" ~ code_arg? ~ space_any? ~ "]]\n" ~
    (!("[[/" ~ space? ~ ^"code") ~ ANY)* ~
    "[[/" ~ space? ~ ^"code" ~ space_any? ~ "]]"
}
code_arg = { space_any ~ (^"type" | ^"language" | ^"lang") ~ space_any? ~ "=" ~ space_any? ~ string }

collapsible = {
    "[[" ~ space? ~ ^"collapsible" ~ collapsible_arg* ~ space_any? ~ "]]\n" ~
    paragraph* ~
    "[[/" ~ space? ~ ^"collapsible" ~ space_any? ~ "]]"
}
collapsible_arg = {
    space_any ~ (
        ^"hidelocation" | ^"show" | ^"hide" | ^"id" | ^"class" | ^"style"
    ) ~ space_any? ~ "=" ~ space_any? ~ string
}

div = {
    "[[" ~ space? ~ ^"div" ~ "_"? ~ div_arg* ~ space_any? ~ "]]\n" ~
    paragraph* ~
    "[[/" ~ space? ~ ^"div" ~ "_"? ~ space_any? ~ "]]"
}
div_arg = { space_any ~ (^"id" | ^"class" | ^"style") ~ space_any? ~ "=" ~ space_any? ~ string }

html = {
    "[[" ~ space? ~ ^"html" ~ "_"? ~ space_any? ~ "]]\n" ~
    (!("[[/" ~ space? ~ ^"html") ~ ANY)* ~
    "[[/" ~ space? ~ ^"html" ~ space_any? ~ "]]"
}

iframe = {
    "[[" ~ space? ~ ^"iframe" ~ "_"? ~ space_any ~ url ~ iframe_arg* ~ space_any? ~ "]]"
}
iframe_arg = { space_any ~ ident ~ space_any? ~ "=" ~ space_any? ~ string }

javascript = {
    "[[" ~ space? ~ (^"js" | ^"javascript") ~ space_any? ~ "]]\n" ~
    (!("[[/" ~ space? ~ (^"js" | ^"javascript")) ~ ANY)* ~
    "[[/" ~ space? ~ (^"js" | ^"javascript") ~ space_any? ~ "]]"
}

quote_block = {
    "[[" ~ space? ~ (^"quote" | ^"blockquote") ~ quote_block_arg* ~ space_any? ~ "]]\n" ~
    paragraphs_internal ~
    "[[/" ~ space? ~ (^"quote" | ^"blockquote") ~ space_any? ~ "]]"
}
quote_block_arg = { space_any ~ (^"id" | ^"class" | ^"style") ~ space_any? ~ "=" ~ space_any? ~ string }
// }}}

// Misc paragraphs {{{
bullet_list = { (" "* ~ "* " ~ list_item)+ }
numbered_list = { (" "* ~ "# " ~ list_item)+ }
list_item = { word+ }
words = {
    (
        "+"{1,6} ~ (" " | "\t") |
        (!"+" ~ "="?)
    ) ~ word+
}
clear_float = { "~"{4,} ~ ("<" | ">")? }
horizontal_line = { "-"{4,} }
// }}}

// Words {{{
word = {
    text |
    raw |
    email |
    color |
    italics |
    strikethrough |
    bold |
    underline |
    subscript |
    superscript |
    monospace |
    anchor |
    anchor_tag |
    css |
    date |
    equation_ref |
    file_ref |
    footnote |
    footnote_block |
    form |
    gallery |
    module |
    note |
    image |
    size |
    span |
    tab_list |
    user |
    link_bare |
    link_page |
    link_url |
    em_dash
}
// }}}

// Dash words {{{
em_dash = { "---" | "--" }
strikethrough = {
    // This is awful but I haven't gotten it to both not produce
    // an error and not return a lazy parse where everything
    // is just surrounded by em dashes.
    "--" ~ (!("--" | "\n") ~ ANY)+ ~ "--"
}
strikethrough_words = { SOI ~ word+ ~ EOI }
// }}}

// Formatting words {{{
color = {
    (
        "##" ~ space? ~ ("#"? ~ ident) ~ space? ~ "|" ~ word* ~ "##"
    ) |
    (
        "[[" ~ space? ~ (^"color" | ^"colour") ~ space ~ ident ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"color" | ^"colour") ~ space? ~ "]]"
    )
}

italics = {
    ("//" ~ word* ~ "//") |
    (
        "[[" ~ space? ~ (^"italics" | ^"i") ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"italics" | ^"i") ~ space? ~ "]]"
    )
}

bold = {
    ("**" ~ word* ~ "**") |
    (
        "[[" ~ space? ~ (^"bold" | ^"b") ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"bold" | ^"b") ~ space? ~ "]]"
    )
}

underline = {
    ("__" ~ word* ~ "__") |
    (
        "[[" ~ space? ~ (^"underline" | ^"u") ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"underline" | ^"u") ~ space? ~ "]]"
    )
}

subscript = {
    (",," ~ word* ~ ",,") |
    (
        "[[" ~ space? ~ (^"subscript" | ^"sub") ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"subscript" | ^"sub") ~ space? ~ "]]"
    )
}

superscript = {
    ("^^" ~ word* ~ "^^") |
    (
        "[[" ~ space? ~ (^"superscript" | ^"sup") ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"superscript" | ^"sup") ~ space? ~ "]]"
    )
}

monospace = {
    ("{{" ~ word* ~ "}}") |
    (
        "[[" ~ space? ~ (^"monospace" | ^"mono") ~ space? ~ "]]" ~
        word* ~
        "[[/" ~ space? ~ (^"monospace" | ^"mono") ~ space? ~ "]]"
    )
}
// }}}

// Block words {{{
anchor = { "[[#" ~ space? ~ ident ~ space? ~ "]]" }

css = {
    "[[" ~ space? ~ (^"css" | ^"style") ~ space_any? ~ "]]\n" ~
    (!("[[/" ~ space? ~ (^"css" | ^"style")) ~ ANY)* ~
    "[[/" ~ space? ~ (^"css" | ^"style") ~ space_any? ~ "]]"
}

date = {
    "[[" ~ space? ~ ^"date" ~ space_any ~ integer ~ date_arg? ~ space_any? ~ "]]"
}
date_arg = @{ space_any ~ ^"format" ~ space_any? ~ "=" ~ space_any? ~ string }

equation_ref = {
    "[[" ~ space? ~ (^"eref" | ^"equationref") ~ space_any ~ ident ~ space_any? ~ "]]"
}

file_ref = {
    "[[" ~ space? ~ link_newtab ~ space? ~ ^"file" ~ space_any ~ file_ident ~
    (space_any? ~ link_separator ~ (space_any? ~ file_name)?)? ~ space_any? ~ "]]"
}
file_name = { (!"]]" ~ ANY)+ }

footnote = {
    "[[" ~ space? ~ ^"footnote" ~ space_any? ~ "]]" ~
    paragraphs_internal ~
    "[[/" ~ space? ~ ^"footnote" ~ space_any? ~ "]]"
}

footnote_block = { "[[" ~ space? ~ ^"footnoteblock" ~ space_any? ~ "]]" }

form = {
    "[[" ~ space? ~ ^"form" ~ space_any? ~ "]]\n" ~
    (!("[[/" ~ space? ~ ^"form") ~ ANY)* ~
    "[[/" ~ space? ~ ^"form" ~ space_any? ~ "]]"
}

// TODO: add arguments -- https://www.wikidot.com/doc-wiki-syntax:images
gallery = { "[[" ~ space? ~ ^"gallery" ~ space_any? ~ "]]" }

module = {
    "[[" ~ space? ~ ^"module" ~ space_any ~ ident ~ module_arg* ~ space_any? ~ "]]" ~
    (
        "\n" ~ (!("[[/" ~ space? ~ ^"module") ~ ANY)* ~
        "[[/" ~ space? ~ ^"module" ~ space_any? ~ "]]"
    )?
}
module_arg = { space_any ~ ident ~ space_any? ~ "=" ~ space_any? ~ string }

note = {
    "[[" ~ space? ~ ^"note" ~ space_any? ~ "]]\n" ~
    paragraph* ~
    "[[/" ~ space? ~ ^"note" ~ space_any? ~ "]]"
}

image = {
    "[[" ~ space? ~ image_alignment? ~ space? ~
    ^"image" ~ space_any ~ file_ident ~
    image_arg* ~ space_any? ~ "]]"
}
image_alignment = {
    space_any? ~ ("f"? ~ ("<" | ">") | "=")?
}
image_arg = {
    space_any ~
    (^"link" | ^"alt" | ^"title" | ^"width" | ^"height" | ^"style" | ^"class" | ^"size") ~
    space_any? ~ "=" ~ space_any? ~ string
}

size = {
    "[[" ~ space? ~ ^"size" ~ space_any ~ size_arg ~ space_any? ~ "]]" ~
    paragraphs_internal ~
    "[[/" ~ space? ~ ^"size" ~ space_any? ~ "]]"
}
size_arg = {
    (ASCII_DIGIT | "."){1,5} ~ space? ~ size_value |
    ^"xx-small" |
    ^"x-small" |
    ^"smaller" |
    ^"larger" |
    ^"small" |
    ^"medium" |
    ^"large" |
    ^"x-large" |
    ^"xx-large"
}
size_value = {
    ^"cm" | ^"mm" | ^"in" | ^"px" | ^"pt" | ^"pc" |
    ^"em" | ^"ex" | ^"ch" | ^"rem" | ^"vw" | ^"vh" |
    ^"vmin" | ^"vmax" | "%"
}

span = {
    "[[" ~ space? ~ ^"span" ~ span_arg* ~ space_any? ~ "]]" ~
    paragraphs_internal ~
    "[[/" ~ space? ~ ^"span" ~ space_any? ~ "]]"
}
span_arg = { space_any ~ (^"id" | ^"class" | ^"style") ~ space_any? ~ "=" ~ space_any? ~ string }

tab_list = {
    "[[" ~ space? ~ (^"tablist" | ^"tabview" | ^"tabs") ~ space_any? ~ "]]" ~ space_any? ~
    (tab ~ space_any?)* ~
    "[[/" ~ space? ~ (^"tablist" | ^"tabview" | ^"tabs") ~ space_any? ~ "]]"
}
tab = {
    "[[" ~ space? ~ ^"tab" ~ space_any ~ tab_name ~ space_any? ~ "]]" ~ space_any? ~
    paragraphs_internal ~
    "[[/" ~ space? ~ ^"tab" ~ space_any? ~ "]]"
}
tab_name = { (!("]]" | "\n") ~ ANY)+ }

user = { "[[" ~ space? ~ "*"? ~ space? ~ ^"user" ~ space ~ ident ~ space? ~ "]]" }
// }}}

// Link words {{{
anchor_tag = {
    "[[" ~ space? ~ ^"a" ~ "_"? ~ anchor_arg* ~ space_any? ~ "]]" ~
    word* ~
    "[[/" ~ space? ~ ^"a" ~ "_"? ~ space_any? ~ "]]"
}
anchor_arg = {
    space ~ (^"href" | ^"name" | ^"id" | ^"class" | ^"style" | ^"target") ~ space? ~
    "=" ~ space? ~ string
}

link_bare = { link_newtab ~ url }

link_page = {
    "[[[" ~ space_any? ~ link_newtab ~ space_any? ~ link_page_href ~
    (space_any? ~ link_separator ~ (space_any? ~ link_page_name)?)? ~ space_any? ~ "]]]"
}
link_separator = { "|" }
link_page_href = { (!("\n" | "|" | "]]]") ~ ANY)+ }
link_page_name = { (!("\n" | "[" | "]]]") ~ ANY)+ }

link_url = {
    "[" ~ !"[" ~ space? ~ link_newtab ~ space? ~
     link_url_href ~ space ~
     link_url_name ~ space? ~ "]"
}
link_url_href = { ^"mailto:"? ~ email | root_url | url }
link_url_name = { (!("\n" | "[" | "]") ~ ANY)+ }

link_newtab = { "*"? }
// }}}

// Misc words {{{
text = {
    (!(
        "@@" |
        "//" |
        "--" |
        "**" |
        "__" |
        ",," |
        "^^" |
        "{{" | "}}" |
        "[[" | "]]" |
        "[[[" | "]]]" |
        "##" |
        "-"{4,} | "~"{4,} |
        "\n" |
        url |
        link_url
    ) ~ ANY)+
}

raw = { ("@@@@@@" | "@@" ~ (!("@@" | "\n") ~ ANY)* ~ "@@") }
// }}}

// Components {{{
hex_digit = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }
space = _{ (" " | "\r" | "\t")+ }
space_any = _{ (" " | "\r" | "\t" | "\n")+ }
integer = @{ "-"? ~ ASCII_DIGIT+ }
file_ident = @{ (!(space | "]]") ~ ANY)+ }
ident = @{ (ASCII_ALPHANUMERIC | "-" | "+" | "_" | "." | "%")+ }
str_inner = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ str_inner)? }
string = @{ "\"" ~ str_inner ~ "\"" }
escape = @{ "\\" ~ ("\"" | "\\" | "r" | "n" | "t" | "0" | "'") }
root_url = @{ "/" ~ (!(" " | "\n" | "|" | "]") ~ ANY)+ }
url = @{ (^"http" ~ ^"s"? | ^"ftp") ~ "://" ~ (!(" " | "\n" | "|" | "]") ~ ANY)+ }
email = @{
    (ASCII_ALPHANUMERIC | "-" | "+" | "_" | ".")+ ~ "@" ~
    (ASCII_ALPHANUMERIC | "-")+ ~ "." ~
    (ASCII_ALPHANUMERIC | ".")+
}
// }}}

// vim: set fdm=marker foldlevel=0:
